plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'

    compileOnlyApi "mezz.jei:jei-${minecraft_version}-common-api:19.21.2.313"
    implementation "mezz.jei:jei-${minecraft_version}-common:19.21.2.313"

    compileOnlyApi "dev.emi:emi-xplat-mojmap:1.1.22+1.21.1:api"
    implementation "dev.emi:emi-xplat-mojmap:1.1.22+1.21.1"

    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

